#! /etc/profiles/per-user/mjs/bin/fish

if test $hostname = terra
    set bat /org/freedesktop/UPower/devices/battery_BAT0
else
    set bat /org/freedesktop/UPower/devices/battery_BAT1
end

if test $hostname = terra
    set ac /org/freedesktop/UPower/devices/line_power_AC
else
    set ac /org/freedesktop/UPower/devices/line_power_ADP1
end

set -l bat_json (
	upower -i $bat |
	jc --upower
)
set -l percentage (echo $bat_json | jq -r '.[0].detail.percentage')
set -l plugged_in (
	upower -i $ac |
	jc --upower |
	jq -r '.[0].detail.online'
)

# set icon and class
set -l icons 󰂎 󰁺 󰁻 󰁼 󰁽 󰁾 󰁿 󰂀 󰂁 󰂂 󰁹 󰢟 󰢜 󰂆 󰂇 󰂈 󰢝 󰂉 󰢞 󰂊 󰂋 󰂅
set -l idx (math "ceil(($percentage + 1) / 10)")
if test $plugged_in = true
    set idx (math "$idx+11")
end

if test $idx -ge 20
    set class "battery charging full"
else if test $idx -ge 15
    set class "battery charging normal"
else if test $idx -ge 12
    set class "battery charging low"
else if test $idx -ge 9
    set class "battery full"
else if test $idx -ge 4
    set class "battery normal"
else
    set class "battery low"
end

echo "{\"percentage\":$percentage,\"icon\":\"$icons[$idx]\",\"class\":\"$class\"}"
